//Some grammar-related directives
#!language      "C";
#whitespaces    ' \t';
#lexeme         integer;
#default action	[* @@ = @1; *];

#left           '+' '-';
#left           '*' '/';

//Defining the grammar
calculator$     -> expression                [* printf( "= %d\n",
                                                  @expression ); *]
                ;

expression      -> expression '+' expression [* @@ = @1 + @3; *]
                | expression '-' expression  [* @@ = @1 - @3; *]
                | expression '*' expression  [* @@ = @1 * @3; *]
                | expression '/' expression  [* @@ = @1 / @3; *]
                | '(' expression ')'         [* @@ = @2; *]
                | integer
                ;

integer         -> '0-9'                     [* @@ = @1 - '0'; *]
                | integer '0-9'              [* @@ = @integer * 10 +
                                                     @2 - '0'; *]
                ;

//End of definition
