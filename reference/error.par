//Some grammar-related directives
#!language      "C";
#whitespaces    ' \t';
#default action [* @@ = @1; *];

#left           '+' '-';
#left           '*' '/';

//Defining the grammar
calculator$     -> expression                [* printf( "= %d\n",
                                                  @expression );
                                                printf( "(%d errors)\n",
                                                  pcb->error_count );
                                             *]
                ;

expression      -> expression '+' expression [* @@ = @1 + @3; *]
                | expression '-' expression  [* @@ = @1 - @3; *]
                | expression '*' expression  [* @@ = @1 * @3; *]
                | expression '/' expression  [* @@ = @1 / @3; *]
                | '(' expression ')'         [* @@ = @2; *]
                | integer
                | &error                     [* printf( "Error: "
                                                    "Expecting integer on "
                                                    "line %d, column %d\n",
                                                    pcb->line, pcb->column );
                                                pcb->error_count++;
                                                @@ = 0;
                                             *]
                ;

integer         -> '0-9'                     [* @@ = @1 - '0'; *]
                | integer '0-9'              [* @@ = @integer * 10 +
                                                     @2 - '0'; *]
                ;

//End of definition
